apply plugin: 'com.android.application'

import java.util.zip.ZipFile
import java.nio.file.Files
import java.nio.file.StandardCopyOption

android {
    namespace "com.fishtankgame.android"
    compileSdk = 36

    ndkVersion "26.1.10909125"

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_17
      targetCompatibility JavaVersion.VERSION_17
      coreLibraryDesugaringEnabled true
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
            assets.srcDirs = ['../assets']
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/robovm/ios/robovm.properties', 'META-INF/INDEX.LIST']
        }
        jniLibs {
            doNotStrip += ["**/*.so"]
        }
    }
    defaultConfig {
        applicationId "com.fishtankgame"
        minSdk = 24
        targetSdk = 36
        versionCode 9
        versionName "1.1.6"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
}

repositories {
    google()
    mavenCentral()
}

configurations { natives }

configurations.configureEach {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}

dependencies {
    implementation project(':core')
    api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"

    // Google Play Billing Library
    implementation "com.android.billingclient:billing:8.2.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}

tasks.register('copyAndroidNatives') {
    inputs.files(configurations.natives)
    def libsDir = layout.projectDirectory.dir("libs")
    outputs.dir(libsDir)

    doLast {
        inputs.files.each { jar ->
            def jarName = jar.name
            def subDir = null
            if (jarName.endsWith("natives-arm64-v8a.jar")) subDir = "arm64-v8a"
            else if (jarName.endsWith("natives-armeabi-v7a.jar")) subDir = "armeabi-v7a"
            else if (jarName.endsWith("natives-x86_64.jar")) subDir = "x86_64"
            else if (jarName.endsWith("natives-x86.jar")) subDir = "x86"

            if (subDir != null) {
                def outputDir = libsDir.dir(subDir).asFile
                outputDir.mkdirs()
                def zipFile = new ZipFile(jar)
                try {
                    def entries = zipFile.entries()
                    while (entries.hasMoreElements()) {
                        def entry = entries.nextElement()
                        if (!entry.isDirectory() && entry.name.endsWith(".so")) {
                            def outFile = new File(outputDir, new File(entry.name).getName())
                            zipFile.getInputStream(entry).withStream { input ->
                                Files.copy(input, outFile.toPath(), StandardCopyOption.REPLACE_EXISTING)
                            }
                        }
                    }
                } finally {
                    zipFile.close()
                }
            }
        }
    }
}

tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach { packageTask ->
    packageTask.dependsOn 'copyAndroidNatives'
}
